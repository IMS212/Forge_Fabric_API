plugins {
    id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false
    id 'maven-publish'
}

subprojects {
    group = project.maven_group
    version = getProperty("mod_ver") + "+" + rootProject.minecraft_version

    apply plugin: 'dev.architectury.loom'
    apply plugin: 'maven-publish'


    base {
        archivesName = project.name
    }

    repositories {
        // Add NeoForged repository.
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }

        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered {
			it.mappings("net.fabricmc:yarn:$rootProject.yarn_mappings:v2")
			it.mappings(file(rootDir.toPath().resolve("12100fix.tiny")))
		}
        neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"
    }

    processResources {
        inputs.property 'version', project.version

        filesMatching('META-INF/neoforge.mods.toml') {
            expand version: project.version
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

// Configure Maven publishing.
    publishing {
		publications {
			maven(MavenPublication) {
				groupId = 'net.fabricmc'
				artifactId = project.name
				version = getProperty("mod_ver") + "+" + rootProject.minecraft_version

				from components.java
			}
		}

		repositories {
			maven {
				name = "GitHubPackages"
				url = "https://maven.pkg.github.com/IMS212/Forge_Fabric_API"
				credentials {
					username = 'IMS212'
					password = System.getenv("GITHUB_TOKEN")
				}
			}
		}
    }

}
